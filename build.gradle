buildscript {
	ext {
		cleanArchitectureVersion = '3.26.1'
		springBootVersion = '3.5.4'
		sonarVersion = '6.3.1.5724'
		jacocoVersion = '0.8.13'
		pitestVersion = '1.19.0-rc.1'
        lombokVersion = '1.18.38'
        mapstructVersion = '1.6.3'
        jakartaValidationVersion = '3.0.2'
	}
}

plugins {
	id 'co.com.bancolombia.cleanArchitecture' version "${cleanArchitectureVersion}"
	id 'org.springframework.boot' version "${springBootVersion}" apply false
	id 'info.solidsoft.pitest' version "${pitestVersion}" apply false
	id 'org.sonarqube' version "${sonarVersion}"
	id 'jacoco'
}

def sonarProps = new Properties()
file("sonar.properties").withInputStream { sonarProps.load(it) }

sonar {
    def modules = subprojects.projectDir.collect { "${it.toString().replace(project.projectDir.toString() + "/", "")}" }
    properties {
        sonarProps.each { key, value ->
            property key, value
        }
        property "sonar.modules", "${modules.join(',')}"
        property "sonar.sources", sonarProps["sonar.sources"] + "," + modules.collect { "${it}/build.gradle" }.join(',')
    }
}

apply from: './main.gradle'
